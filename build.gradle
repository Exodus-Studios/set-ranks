plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
}

group 'com.reussy.development.setranks.plugin'
version '1.0.0'

repositories {
    mavenCentral()

    maven { url = uri('https://jitpack.io') }

    maven { url = 'https://repo.papermc.io/repository/maven-public/' }

    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }

}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'dev.triumphteam:triumph-gui:3.1.4' // GUI library
    implementation 'com.github.cryptomorin:XSeries:9.1.0' // Support for materials and sounds across versions
    implementation fileTree(include: ['IridiumColorAPI.jar'], dir: 'libs') // IridiumColorAPI to hex colors
    implementation "io.papermc:paperlib:1.0.7" // PaperLib

    compileOnly 'com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly("net.luckperms:api:5.4") // Manage permissions.
    compileOnly 'me.clip:placeholderapi:2.11.2' // PlaceholderAPI
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    // No podemos usar el nombre de rootProject.name porque contiene Exodus.
    final String archiveName = "Set Ranks - v" + version.toString() + '.jar'
    getArchiveFileName().set(archiveName)

    relocate "org.jetbrains.annotations", "com.reussy.development.setranks.plugin.libs.annotations"
    relocate "dev.triumphteam", "com.reussy.development.setranks.plugin.libs.gui"
    relocate "com.cryptomorin.xseries", "com.reussy.development.setranks.plugin.libs.xseries"
    relocate "com.iridium.iridiumcolorapi", "com.reussy.development.setranks.plugin.libs.color"
    relocate "io.papermc", "com.reussy.development.setranks.plugin.libs.paper"
    relocate "net.kyori", "com.reussy.development.setranks.plugin.libs.kyori"
    relocate "org.intellij", "com.reussy.development.setranks.plugin.libs.intellij"
    minimize()
}